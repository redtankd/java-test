task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
    distributionUrl = "../gradle-${gradleVersion}-bin.zip"
}

allprojects {
    version = '1.0'
}


/////////////////////////////////////////////////////////////////////
// java projects: common configuration

// configure(subprojects.findAll { ['xtend'].contains(it.name) }) {
subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    eclipseJdt << {
        File f1 = file('.settings/org.eclipse.core.resources.prefs')
        f1.write('eclipse.preferences.version=1\n')
        f1.append('encoding/<project>=utf-8')

        File f2 = file('.settings/org.eclipse.core.runtime.prefs')
        f2.write('eclipse.preferences.version=1\n')
        f2.append('line.separator=\\n')
    }

    sourceSets.main.resources.srcDir 'src/main/java' 
    sourceSets.test.resources.srcDir 'src/test/java' 

    dependencies {
        // log system
        def slf4jVersion = '1.7.7'
        compile "ch.qos.logback:logback-classic:1.1.2"
        compile "org.codehaus.janino:janino:2.6.1"
        compile "org.slf4j:slf4j-api:${slf4jVersion}"
        compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
        compile "org.slf4j:jul-to-slf4j:${slf4jVersion}"

        testCompile "junit:junit:4.11"
    }

    configurations {
        all*.exclude module: "commons-logging"
        all*.exclude module: "log4j"
        all*.exclude module: "mina-core"
    }

    repositories {
        mavenCentral()
    }
}


/////////////////////////////////////////////////////////////////////
// java projects: xtend configuration

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.xtend:xtend-gradle-plugin:0.3.3'
    }
}

// configure(subprojects.findAll { ['xtend'].contains(it.name) }) {
subprojects {
    apply plugin: 'org.xtend.xtend'

    xtend {
        xtendAsPrimaryDebugSource = true
        hideSyntheticVariables = false
        encoding = "UTF-8"
    }
}


/////////////////////////////////////////////////////////////////////
// java projects: the java dependencies management

ext.jSpringVersion    = '4.1.2.RELEASE'
ext.jSpringIntVersion = '4.1.0.RELEASE'
ext.jJettyVersion     = '9.2.5.v20141112'
ext.jUndertowVersion  = '1.1.0.Final'

ext.jDeps = [ // Groovy map literal
    // language
    xtend:       "org.eclipse.xtend:org.eclipse.xtend.lib:2.7.3",
    asm:         "org.ow2.asm:asm-all:5.0.3",

    // spring
    springContext: "org.springframework:spring-context:$jSpringVersion",
    springJdbc:    "org.springframework:spring-jdbc:$jSpringVersion",
    springWebmvc:  "org.springframework:spring-webmvc:$jSpringVersion",
    springTest:    "org.springframework:spring-test:$jSpringVersion",
    springInt:     "org.springframework.integration:spring-integration-core:$jSpringIntVersion",
    springShell:   "org.springframework.shell:spring-shell:1.1.0.RELEASE",

    // middleware
    activemq:        "org.apache.activemq:activemq-broker:5.10.0",
    jettyServer:     "org.eclipse.jetty:jetty-server:$jJettyVersion",
    jettyServlet:    "org.eclipse.jetty:jetty-servlet:$jJettyVersion",
    jettyWebapp:     "org.eclipse.jetty:jetty-webapp:$jJettyVersion",
    undertowServlet: "io.undertow:undertow-servlet:$jUndertowVersion",

    // network
    netty: "io.netty:netty-all:4.0.24.Final",
    sshd:  "org.apache.sshd:sshd-core:0.13.0",

    // util
    commonsConf:     "commons-configuration:commons-configuration:1.10",
    config:          "com.typesafe:config:1.2.1",
    jacksonDatabind: "com.fasterxml.jackson.core:jackson-databind:2.4.3"
]