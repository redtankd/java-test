allprojects {
    version = '1.0'
}

/////////////////////////////////////////////////////////////////////
// java projects: common configuration

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    dependencies {
        // log system
        def slf4jVersion = '1.7.21'
        compile "org.slf4j:slf4j-api:${slf4jVersion}"
        compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
        compile "org.slf4j:jul-to-slf4j:${slf4jVersion}"
        compile "ch.qos.logback:logback-classic:1.1.7"
        compile "org.codehaus.janino:janino:2.7.8"

        testCompile "junit:junit:4.12"
    }

    configurations {
        // *. is groovy's spread operator
        all*.exclude module: "commons-logging"
        all*.exclude module: "log4j"
        all*.exclude module: "mina-core"
    }

    sourceSets.main.resources.srcDir 'src/main/java' 
    sourceSets.test.resources.srcDir 'src/test/java' 

    eclipseJdt << {
        File f1 = file('.settings/org.eclipse.core.resources.prefs')
        f1.write('eclipse.preferences.version=1\n')
        f1.append('encoding/<project>=utf-8')

        File f2 = file('.settings/org.eclipse.core.runtime.prefs')
        f2.write('eclipse.preferences.version=1\n')
        f2.append('line.separator=\\n')
    }

    repositories {
        mavenCentral()
        maven {
            url "http://maven.oschina.net/content/groups/public/"
        }
        
    }
}

buildscript {
    repositories {
        maven {
            url "http://maven.oschina.net/content/groups/public/"
        }
        mavenCentral()
    }
}

/////////////////////////////////////////////////////////////////////
// java projects: the java dependencies management

ext.jSpringVersion    = '4.2.5.RELEASE'
ext.jSpringIntVersion = '4.2.5.RELEASE'
ext.jJettyVersion     = '9.3.8.v20160314'

ext.jDeps = [ // Groovy map literal
    // language
    xtend: "org.eclipse.xtend:org.eclipse.xtend.lib:2.9.2",

    // spring
    springContext: "org.springframework:spring-context:$jSpringVersion",
    springJdbc:    "org.springframework:spring-jdbc:$jSpringVersion",
    springWebmvc:  "org.springframework:spring-webmvc:$jSpringVersion",
    springTest:    "org.springframework:spring-test:$jSpringVersion",
    springInt:     "org.springframework.integration:spring-integration-core:$jSpringIntVersion",
    springShell:   "org.springframework.shell:spring-shell:1.1.0.RELEASE",

    // middleware
    activemq:        "org.apache.activemq:activemq-broker:5.13.2",
    jettyServer:     "org.eclipse.jetty:jetty-server:$jJettyVersion",
    jettyServlet:    "org.eclipse.jetty:jetty-servlet:$jJettyVersion",
    jettyWebapp:     "org.eclipse.jetty:jetty-webapp:$jJettyVersion",
    undertowServlet: "io.undertow:undertow-servlet:1.0.2.Final",

    // network
    netty: "io.netty:netty-all:4.0.36.Final",
    sshd:  "org.apache.sshd:sshd-core:1.2.0",

    // util
    asm:             "org.ow2.asm:asm-all:5.0.3",
    commonsConf:     "commons-configuration:commons-configuration:1.10",
    config:          "com.typesafe:config:1.3.0",
    jacksonDatabind: "com.fasterxml.jackson.core:jackson-databind:2.7.3"
]